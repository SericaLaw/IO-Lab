DATA SEGMENT 'DATA'
    MUS_RANK    DW  262, 294, 330, 349, 392, 440, 494, 523
    ; 10 000 000 HZ
    CLK_HI  DW  98H
    CLK_LO  DW  9680H
DATA ENDS
CODE SEGMENT 'CODE'
    ASSUME DS:DATA, CS:CODE
START:
    MOV AX, 0080H
    MOV DS, AX
    ; INIT DISPLAY
    MOV AX, 0FFH
    OUT 24H, AX
    ; DISABLE BUZZER
    XOR AL, AL
    OUT 60H, AL
    ; INIT 8254
    MOV AL, 00110110B
    OUT 46H, AL
INPUT:
    ; READ KEYBOARD
    IN AX, 30H
    TEST AL, 20H    ; WHEN HOLDING THE KEY
    JZ DISABLE_BUZZER
    AND AX, 000FH
    CMP AX, 0
    JZ DISABLE_BUZZER
    CMP AX, 9H
    JNC DISABLE_BUZZER   ; IF INPUT > 9, INPUT AGAIN
    ; DISPLAY INPUT
    OUT 20H, AX
    LEA SI, MUS_RANK
    DEC AX
    ADD SI, AX   ; SI = SI + (AX - 1) * 2
    ADD SI, AX
    MOV DX, CLK_HI
    MOV AX, CLK_LO
    MOV DI, [SI]
    DIV DI  ; CALC INIT VALUE
    OUT 22H, AX ; DISPLAY IT
    OUT 40H, AL
    MOV AL, AH
    OUT 40H, AL
    ; ENABLE BUZZER
    MOV AL, 1
    OUT 60H, AL
    JMP INPUT
DISABLE_BUZZER:
    ; DISABLE BUZZER
    XOR AX, AX
    OUT 60H, AL
    ; CLEAR DISPLAY
    OUT 20H, AX
    OUT 22H, AX
    JMP INPUT
CODE ENDS
    END START